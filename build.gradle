plugins {
    id "com.diffplug.spotless" version "6.14.1" apply false
}

allprojects {

    group 'com.solelydev.marketup'

    repositories {
        mavenCentral()
    }
}

ext {
    lombokVersion = '1.18.24'
    vavrVersion = '0.10.4'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.diffplug.spotless'

    sourceCompatibility = JavaVersion.VERSION_19
    targetCompatibility = JavaVersion.VERSION_19

    dependencies {
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "io.vavr:vavr:$vavrVersion"
    }

    test {
        useJUnitPlatform()
    }

    check {
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn check
        finalizedBy jacocoTestCoverageVerification
    }

    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport

        violationRules {

            rule {
                excludes = ['application']
            }

            rule {
                enabled false
                limit {
                    minimum = 0
                }
            }
        }
    }

    spotless {
        java {
            googleJavaFormat()
            formatAnnotations()
        }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs += '--enable-preview'
        }
    }

    tasks.withType(Test).tap {
        configureEach {
            jvmArgs += '--enable-preview'
        }
    }

    tasks.withType(JavaExec).tap {
        configureEach {
            jvmArgs += '--enable-preview'
        }
    }

}